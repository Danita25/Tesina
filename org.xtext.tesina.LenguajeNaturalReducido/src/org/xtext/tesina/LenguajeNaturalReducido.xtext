grammar org.xtext.tesina.LenguajeNaturalReducido with org.eclipse.xtext.common.Terminals

generate lenguajeNaturalReducido "http://www.xtext.org/tesina/LenguajeNaturalReducido"

Documento:
	encabezado=Literal FinOracion
	oraciones+=Oracion*;

Oracion:
	contenido=Contenido FinOracion;

FinOracion:
	'.\r\n';

Contenido:
	condicional='si'? //para el implies
	simple=Simple
	composicion=Composicion?;

Simple:
	expresion_izq=Expresion
	expresion_der=ExpresionDerecha?;

ExpresionDerecha:
	comparacion=Comparacion
	expresion=Expresion;

Composicion:
	nexo=Nexo
	contenido=Contenido;

Nexo:
	nexo=('y' | 'o' | 'entonces');

Expresion:
	expresion=(Literal | Propiedad);

Comparacion:
	afirmacion=Afirmacion
	operacion=Operacion;

Propiedad:
	atributo+=(Atributo)+
	termino=(Termino)?
	iteracion+=(Iteracion)*; //coleccion

Iteracion:
	condicion=('tal que' | 'satisfacen que' | 'existe uno/una tal que')
	contenido=Contenido;

Atributo:
	determinante=('el/la' | 'todos los/las' | 'entre los/las')
	cuantitativo=('cantidad de')?
	nombre=ID
	enlace='de';

Termino:
	indeterminante='un/una'
	contexto=ID;

Afirmacion:
	(negacion='no')? afirmacion='es';

Operacion:
	operacion=('mayor que' | 'menor que' | 'igual a' | 'mayor o igual a' | 'menor o igual a' | 'distinto de');

Literal:
	literal=STRING;
