import Biblioteca: '../model/Biblioteca.ecore'

package Biblioteca

context Biblioteca
	inv poseeDireccion: direccion<>''
	inv poseeLibros: libros->select(l| l.activo=true)->notEmpty()
	inv librosNoRepetidos: libros->forAll(l1,l2|l1<>l2 and l1.autor=l2.autor implies l1.titulo<>l2.titulo) 
	inv autoresNoRepetidos: autores->forAll(a1,a2|a1<>a2 implies a1.nombreCompleto<>a2.nombreCompleto) 
	
context Libro 
	inv poseeCopias: copias>0
	inv tieneAutor: autor <> null	
	inv suficientesCopias: (Prestamo.allInstances()->select(p1| p1.fechaDeDevolucion=null and p1.libro=self))->size()<= copias
	

context Prestamo 
	inv tieneFechaInicio : fechaInicio<>null
	inv tieneFechaFin : fechaFin<>null
	inv tieneSocio : socio<>null  
	inv tieneLibro : libro<>null
--	inv fechaValidas: fechaInicio<=fechaFin
	inv fechasSolicitudValidas : 
		if(fechaInicio<>null and fechaFin<>null)then
				fechaInicio<=fechaFin
		else
			true
		endif
		
	
context Multa
	inv tienePrestamo:prestamo<>null
	inv fechaInicio : fechaInicio<>null
	
	
context Socio	
	--Obtener todos los prestamos del socio activos y no activos
	def: getAllPrestamosSocio() : Set(Prestamo) = Prestamo.allInstances()->select(p| p.socio=self)->asSet()
	--Obtener todos las multas del socio activas y no activas ordenadas.
	def:getAllMultas() : OrderedSet(Multa) = Multa.allInstances()->select(m|m.prestamo.socio=self)->asOrderedSet()
	inv mayorEdad: edad > 18  
	inv tieneNumeroSocio: numeroSocio>0
	inv tieneNombreCompleto: nombreCompleto<>''
	--el socio al menos tiene que haber realizo un prestamo, sino no existe el socio
	inv poseePrestamo:getAllPrestamosSocio()->size()>0
	inv prestamosActivos: let prestamos:Boolean = (getAllPrestamosSocio()->select(p1| p1.fechaDeDevolucion=null))->size()<=3 in
	if prestamos then 
		true
	 else 
	 	false
	 endif

context Socio::devolverEjemplar(codigo:Integer, fechaActual :ecore::EDate) : Prestamo
	pre: Prestamo.allInstances()->select(p1| p1.fechaDeDevolucion=null and p1.socio=self)->notEmpty()
	post: Prestamo.allInstances()->select( p | p.libro.codigo = codigo and p.socio=self and p.fechaDeDevolucion=null).fechaDeDevolucion = fechaActual
	

context Socio::existeSocio(numeroDeSocio:String):Boolean
	body: Socio.allInstances()->exists(numeroDeSocio=numeroDeSocio)

--para asegurar id de socio unico
context Socio::numeroSocio : String init:
	Socio::uniqueID()

context Libro::codigo : String init:
	Libro::uniqueID()
	

context Autor
	inv tieneNombre: nombreCompleto<>''
	inv poseeObras: not obras->isEmpty()


context Socio::solicitarEjemplar(libro:Libro):Prestamo
	pre noPoseeMultas: getAllMultas()->select(m| m.fechaFin=null)->isEmpty()
	pre poseeLimiteXPrestamo: (getAllPrestamosSocio()->select(p1| p1.fechaDeDevolucion=null))->size()<3
	pre libroActivo: libro.activo
	pre libroDisponible: (Prestamo.allInstances()->select(p|p.fechaDeDevolucion=null and p.libro=libro)->size()<libro.copias)
	post prestamosPost: getAllPrestamosSocio()->size() > getAllPrestamosSocio()->size()@pre
endpackage